import:
    org.bukkit.Bukkit
    org.bukkit.event.player.PlayerItemHeldEvent
    org.bukkit.Material


on right click with golden shovel:
    player has permission "claimchunk.claim"
    target is not set
    if player is not sneaking:
        cancel event
        Bukkit.dispatchCommand(player, "claim target")
    else:
        cancel event
        Bukkit.dispatchCommand(player, "abandonclaim target")


on right click with stick:
    player has permission "claimchunk.chunkinfo"
    target is not set
    if player is not sneaking:
        cancel event
        Bukkit.dispatchCommand(player, "chunkinfo target")


on PlayerItemHeldEvent:
    set {_player} to event.getPlayer()
    set {_oldSlotItemType} to {_player}.getInventory().getItem(event.getPreviousSlot()).getType()
    set {_newSlotItemType} to {_player}.getInventory().getItem(event.getNewSlot()).getType()

    if {_newSlotItemType} is Material.GOLDEN_SHOVEL:
        {_player} has permission "claimchunk.claim"
        send action bar "<light blue>황금 삽을 사용해 지역을 보호하거나, 웅크린 상태에서 사용해 보호를 해제할 수 있습니다" to {_player}
    else if {_newSlotItemType} is Material.STICK:
        {_player} has permission "claimchunk.chunkinfo"
        send action bar "<light blue>막대기를 사용해 지역이 보호되었는지 확인할 수 있습니다" to {_player}
    else if {_oldSlotItemType} is Material.GOLDEN_SHOVEL:
        send action bar "" to {_player}
    else if {_oldSlotItemType} is Material.STICK:
        send action bar "" to {_player}